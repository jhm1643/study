plugins {
    id 'java-library'
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

subprojects {
    group = 'com.kabang'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        //swagger
        implementation 'io.springfox:springfox-boot-starter:3.0.0'
        implementation 'io.springfox:springfox-swagger-ui:3.0.0'

        //lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.apache.commons:commons-lang3:3.11'

        //validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }

    test{
        useJUnitPlatform()
    }
}

project(':module-common'){
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    }
}

project(':module-domain'){
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        //database
        api 'com.h2database:h2'

        //jpa
        api 'org.springframework.boot:spring-boot-starter-data-jpa'

        //mapstruct
        implementation 'org.mapstruct:mapstruct:1.4.2.Final'
        annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

        //querydsl
        implementation 'com.querydsl:querydsl-jpa'
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"

        implementation project(':module-common')
    }
}

project(':module-application'){
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'

        implementation project(':module-domain')
        implementation project(':module-external')
        implementation project(':module-common')
    }
}

project(':module-batch'){
    dependencies {
        //database
        api 'com.h2database:h2'

        //querydsl
        implementation 'com.querydsl:querydsl-jpa'

        //batch
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        testImplementation("org.springframework.batch:spring-batch-test")

        implementation project(':module-domain')
        implementation project(':module-common')
    }
}

project(':module-external'){
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-aop'

        //circuit
        implementation 'io.github.resilience4j:resilience4j-spring-boot2:1.7.1'
        implementation 'io.github.resilience4j:resilience4j-circuitbreaker:1.7.1'
        //httpclient
        implementation 'org.apache.httpcomponents:httpclient:4.5.13'

        implementation project(':module-common')
    }
}